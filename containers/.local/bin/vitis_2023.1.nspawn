#!/bin/env zsh

local app=${0:t}
local app_path=/tools/Xilinx/Vitis/2023.1/bin/vitis
local argv=($*)
local home=/home/$app
local host_conf=${XDG_CONFIG_HOME:-$HOME/.config}
local conf=$home/.config
local host_data=/usr/share
local data=$home/.local/share

local container_options=(--as-pid2 --machine=$app --user=$app --chdir=$home)
local container_envs=(--setenv=XDG_RUNTIME_DIR=/run/host)

# Network
local container_binds=(--bind-ro=/etc/resolv.conf)

# Working Dir
container_binds+=(--bind=$HOME/Workspace/code/Xilinx:$home/Xilinx)

# Tray
local host_dbus=${DBUS_SESSION_BUS_ADDRESS#unix:path=}
local container_dbus=/run/host/bus
container_binds+=(--bind-ro=$host_dbus:$container_dbus)
container_envs+=(--setenv=DBUS_SESSION_BUS_ADDRESS=unix:path=$container_dbus)

# Sound
local host_pulse=/run/user/$UID/pulse
local container_pulse=/run/host/pulse
container_binds+=(--bind-ro=$host_pulse:$container_pulse)
container_envs+=(--setenv=PULSE_SERVER=unix:$container_pulse/native)

# Icons
container_binds+=(--bind-ro=$host_data/icons:$data/icons)
container_envs+=(--setenv=XCURSOR_PATH=$data/icons)

# Fonts
container_binds+=(--bind-ro=$host_data/fonts:$data/fonts --bind-ro=$host_conf/fontconfig:$conf/fontconfig)
# container_envs+=(--setenv=_JAVA_OPTIONS="-Dawt.useSystemAAFontSettings=lcd -Dswing.aatext=true -Dsun.java2d.xrender=true")
container_envs+=(--setenv=_JAVA_OPTIONS="-Dawt.useSystemAAFontSettings=lcd")

# Display
# X.org
# see:https://wiki.archlinux.org/title/Systemd-nspawn#Use_an_X_environment
container_binds+=(--bind-ro=/tmp/.X11-unix/)
container_envs+=(--setenv=DISPLAY=$DISPLAY)
# see:https://wiki.archlinux.org/title/Systemd-nspawn#Avoiding_xhost
local xauth_file=/tmp/xauth-$app
touch $xauth_file
xauth nextract - "$DISPLAY" | sed -e 's/^..../ffff/' | xauth -f "$xauth_file" nmerge -
container_options+=(--bind=$xauth_file --setenv=XAUTHORITY=$xauth_file)
# Wayland
# see:https://discuss.linuxcontainers.org/t/howto-use-the-hosts-wayland-and-xwayland-servers-inside-containers/8765
if [[ -d $WAYLAND_DISPLAY ]] {
    local host_wayland=/run/user/$UID/wayland-0
    local container_wayland=/run/host/wayland-0
    container_binds+=(--bind=$host_wayland:$container_wayland)
    container_envs+=(--setenv=WAYLAND_DISPLAY=$WAYLAND_DISPLAY)
}

# Device
container_binds+=(--bind=/dev/dri/ --property=DeviceAllow='char-drm rw') # Graphic cards
container_binds+=(--bind=/dev/input/ --property=DeviceAllow='char-input r') # Joysticks
container_binds+=(--bind=/dev/bus/usb/ --property=DeviceAllow='char-usb_device rwm') # Graphic cards

#print -l $container_options

pkexec systemd-nspawn $container_options $container_binds $container_envs $app_path $argv

