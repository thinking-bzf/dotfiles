#!/bin/env zsh

local app=${0:t}
local argv=($*)
local home=/home/$app
local host_conf=${XDG_CONFIG_HOME:-$HOME/.config}
local conf=$home/.config
local host_data=/usr/share
local data=$home/.local/share

local container_options=(--as-pid2 --machine=$app --user=$app --chdir=$home)

# Network
container_options+=(--bind-ro=/etc/resolv.conf)

# Tray
local host_dbus=${DBUS_SESSION_BUS_ADDRESS#unix:path=}
local container_dbus=/run/user/host/bus
container_options+=(--bind-ro=$host_dbus:$container_dbus --setenv=DBUS_SESSION_BUS_ADDRESS=unix:path=$container_dbus)

# Sound
local host_pulse=/run/user/$UID/pulse
local container_pulse=/run/user/host/pulse
container_options+=(--bind-ro=$host_pulse:$container_pulse --setenv=PULSE_SERVER=unix:$container_pulse/native)

# Icons
container_options+=(--bind-ro=$host_data/icons:$data/icons --setenv=XCURSOR_PATH=$data/icons)

# Fonts
container_options+=(--bind-ro=$host_data/fonts:$data/fonts --bind-ro=$host_conf/fontconfig:$conf/fontconfig)

# Display
# see:https://wiki.archlinux.org/title/Systemd-nspawn#Use_an_X_environment
container_options+=(--bind-ro=/tmp/.X11-unix/ --setenv=DISPLAY=$DISPLAY)
# see:https://wiki.archlinux.org/title/Systemd-nspawn#Avoiding_xhost
local xauth_file=/tmp/xauth-$app
touch $xauth_file
xauth nextract - "$DISPLAY" | sed -e 's/^..../ffff/' | xauth -f "$xauth_file" nmerge -
container_options+=(--bind=$xauth_file --setenv=XAUTHORITY=$xauth_file)

# Device
container_options+=(--bind=/dev/dri/ --property=DeviceAllow='char-drm rw') # Graphic cards
container_options+=(--bind=/dev/input/ --property=DeviceAllow='char-input r') # Joysticks

#print -l $container_options

pkexec systemd-nspawn $container_options $app $argv

